config {
  type: "operations",
  hasOutput: true
}

js {
  const finalTableName = name();
  const fullyQualifiedTempTableName = resolve(`${finalTableName}_temp`);
  const fullyQualifiedFinalTableName = self();
}

-- Delete the temporary table, if it exists (perhaps from a previous run).
DROP TABLE IF EXISTS ${fullyQualifiedTempTableName} CASCADE;

---

-- Create the temporary table.
CREATE TABLE ${fullyQualifiedTempTableName}
(
  country VARCHAR(MAX),
  revenue FLOAT
);

---

-- Copy data from S3 into the temporary table.
COPY ${fullyQualifiedTempTableName}
FROM 's3://dataform-integration-tests-us-east-n-virginia/sample-data/sample_data' 
IAM_ROLE 'arn:aws:iam::161427895535:role/RedshiftS3ReadRole'
IGNOREHEADER 1
DELIMITER ',';

---

-- Delete the output table, if it exists.
DROP TABLE IF EXISTS ${fullyQualifiedFinalTableName} CASCADE;

---

-- Finally, rename the temporary table to the output table.
ALTER TABLE ${fullyQualifiedTempTableName} RENAME TO ${finalTableName};
