config {
  type: "operations",
  hasOutput: true
}
 
-- Create a temp table to load the s3 data into with the correct schema.
create or replace table ${resolve(`${name}_temp`)}
(
  country string,
  revenue float
);

---
-- Copy the data from s3 into the temporary table.
copy into ${resolve(`${name}_temp`)}
from 's3://dataform-integration-tests/sample-data/sample_data.csv'         
file_format = (
  type=csv,
  skip_header=1,
  field_optionally_enclosed_by='"'
)
;

---
-- Drop the actual table.
drop table if exists ${self()} cascade;

---
-- Rename the temporary table to the actual table name.
alter table ${resolve(`${name}_temp`)} rename to ${self()}
